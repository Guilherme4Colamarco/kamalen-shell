pragma Singleton
import Quickshell
import Quickshell.Io
import QtQuick

Singleton {
    id: ui

    property string activeDropdown: ""
    property bool transparencyEnabled: true
    property real barOpacity: transparencyEnabled ? 0.72 : 1
    property string barMode: "fixed"
    property bool dndEnabled: false
    property bool darkMode: true
    property bool darkModeLocked: false
    property int pfpIndex: 0
    property int borderRadius: 16
    property bool locked: false
    property bool powerMenuVisible: false
    property bool layoutMenuVisible: false
    property bool clipboardMenuVisible: false

    property int volume: 50
    property bool muted: false
    property int brightness: 100

    property var notifications: []
    property int _nid: 0

    signal notificationReceived(int nid, string app, string title, string body)

    property var appUsage: ({})

    property string currentPlayer: ""
    property string lastActivePlayer: ""
    property string mediaTitle: ""
    property string mediaArtist: ""
    property string mediaDisplay: ""
    property string mediaState: "stopped"
    property real mediaPos: 0
    property real mediaLen: 0
    property string mediaArtUrl: ""
    property bool hasMedia: mediaState === "playing" || mediaState === "paused"
    property bool blockMediaPosUpdate: false
    property var cava: [0,0,0,0,0,0,0,0,0,0,0,0]
    property bool cavaDecaying: false
    property int gifIndex: 0
    property int mediaDisplayMode: 0
    property bool mediaVinylWithArt: true
    property string animationProfile: "bubbly"
    property string blurProfile: "balanced"

    property string wallhavenApiKey: ""
    property string wallhavenSorting: "relevance"
    property string wallhavenCategories: "111"

    property string _settingsPath:    Quickshell.env("HOME") + "/.config/quickshell/state/settings.json"
    property string _appUsagePath:    Quickshell.env("HOME") + "/.config/quickshell/state/app_usage.json"
    property string _kittyColorsPath: Quickshell.env("HOME") + "/.cache/qs/kitty-colors.conf"
    property string _mangoConfigPath: Quickshell.env("HOME") + "/.config/mango/config.conf"
    property string _gifPath:         Quickshell.env("HOME") + "/.config/quickshell/assets/gifs"

    property int _pendingVolume: -1
    property string _savedLayout: ""

    function toHex(c) {
        var r = Math.round(c.r * 255).toString(16).padStart(2, "0")
        var g = Math.round(c.g * 255).toString(16).padStart(2, "0")
        var b = Math.round(c.b * 255).toString(16).padStart(2, "0")
        return "#" + r + g + b
    }

    function interpolateColor(c1, c2, t) {
        return Qt.rgba(
            c1.r + (c2.r - c1.r) * t,
            c1.g + (c2.g - c1.g) * t,
            c1.b + (c2.b - c1.b) * t,
            1.0
        )
    }

    Component.onCompleted: {
        loadSettings()
        loadAppUsage()
        ensureCacheDir.running = true
        _mediaCmd = _buildMediaCmd()
    }

    Process {
        id: ensureCacheDir
        command: ["bash", "-c", "mkdir -p ~/.cache/qs"]
    }

    Connections {
        target: Colors
        function onColorsChanged() {
            interpolateKittyColors()
            applyDelay.restart()
        }
    }

    Timer {
        id: applyDelay
        interval: 300
        onTriggered: {
            writeKittyConf()
            updateMangoBorderColors()
            if (Colors._lastParsedData) {
                Colors.writeNvimColors(Colors._lastParsedData)
                Colors.writeGtkColors(Colors._lastParsedData)
            }
        }
    }

    property int _kittyStep: 0
    property int _kittySteps: 10

    function interpolateKittyColors() {
        _kittyStep = 0
        kittyInterpolateTimer.restart()
    }

    Timer {
        id: kittyInterpolateTimer
        interval: 30
        repeat: true
        onTriggered: {
            if (_kittyStep >= _kittySteps) {
                stop()
                return
            }

            var t = _kittyStep / (_kittySteps - 1)
            t = t * t * (3.0 - 2.0 * t)

            var bg      = interpolateColor(Colors._prevBg,      Colors.bg,      t)
            var fg      = interpolateColor(Colors._prevFg,      Colors.fg,      t)
            var accent  = interpolateColor(Colors._prevAccent,  Colors.accent,  t)
            var surface = interpolateColor(Colors._prevSurface, Colors.surface, t)
            var dim     = interpolateColor(Colors._prevDim,     Colors.dim,     t)
            var red     = interpolateColor(Colors._prevRed,     Colors.red,     t)
            var green   = interpolateColor(Colors._prevGreen,   Colors.green,   t)
            var yellow  = interpolateColor(Colors._prevYellow,  Colors.yellow,  t)

            var bgHex      = toHex(bg)
            var fgHex      = toHex(fg)
            var accentHex  = toHex(accent)
            var surfaceHex = toHex(surface)
            var dimHex     = toHex(dim)
            var redHex     = toHex(red)
            var greenHex   = toHex(green)
            var yellowHex  = toHex(yellow)

            kittyStepProc.command = ["bash", "-c",
                "for s in /tmp/kitty-socket-*; do " +
                "kitty @ --to unix:$s set-colors " +
                "foreground=" + fgHex + " " +
                "background=" + bgHex + " " +
                "cursor=" + accentHex + " " +
                "selection_foreground=" + bgHex + " " +
                "selection_background=" + accentHex + " " +
                "color0=" + surfaceHex + " " +
                "color8=" + dimHex + " " +
                "color1=" + redHex + " " +
                "color9=" + redHex + " " +
                "color2=" + greenHex + " " +
                "color10=" + greenHex + " " +
                "color3=" + yellowHex + " " +
                "color11=" + yellowHex + " " +
                "color4=" + accentHex + " " +
                "color12=" + accentHex + " " +
                "color5=" + accentHex + " " +
                "color13=" + accentHex + " " +
                "color6=" + accentHex + " " +
                "color14=" + accentHex + " " +
                "color7=" + fgHex + " " +
                "color15=" + fgHex +
                " 2>/dev/null & done; wait"]
            kittyStepProc.running = true

            _kittyStep++
        }
    }

    function lock() {
        locked = true
    }

    function togglePowerMenu() {
        powerMenuVisible = !powerMenuVisible
    }

    function showPowerMenu() {
        powerMenuVisible = true
    }

    function hidePowerMenu() {
        powerMenuVisible = false
    }

    function toggleLayoutMenu() {
        layoutMenuVisible = !layoutMenuVisible
    }

    function showLayoutMenu() {
        layoutMenuVisible = true
    }

    function hideLayoutMenu() {
        layoutMenuVisible = false
    }

    function toggleClipboardMenu() {
        clipboardMenuVisible = !clipboardMenuVisible
    }

    function showClipboardMenu() {
        clipboardMenuVisible = true
    }

    function hideClipboardMenu() {
        clipboardMenuVisible = false
    }

    function toggleDropdown(name) {
        activeDropdown = activeDropdown === name ? "" : name
    }

    function closeDropdowns() {
        activeDropdown = ""
    }

    function addNotification(app, title, body) {
        if (title === "" && body === "") return
        var id = _nid++
        var list = notifications.slice()
        list.unshift({ id: id, app: app, title: title, body: body, time: Date.now() })
        if (list.length > 50) list = list.slice(0, 50)
        notifications = list
        notificationReceived(id, app, title, body)
    }

    function dismissNotif(id) {
        notifications = notifications.filter(n => n.id !== id)
    }

    function clearNotifs() {
        notifications = []
    }

    function dismissGroup(app) {
        notifications = notifications.filter(n => n.app !== app)
    }

    function toggleTransparency() {
        transparencyEnabled = !transparencyEnabled
        applyKittyOpacity()
        writeKittyConf()
        updateMangoOpacity()
        saveSettings()
    }

    function toggleDarkMode() {
        darkMode = !darkMode
        Colors.applyCurrentMode(darkMode)
        saveSettings()
    }

    function toggleDarkModeLock() {
        darkModeLocked = !darkModeLocked
        saveSettings()
    }

    function toggleDnd() {
        dndEnabled = !dndEnabled
        saveSettings()
    }

    function setPfpIndex(idx) {
        pfpIndex = idx
        saveSettings()
    }

    function setVolume(v) {
        volume = v
        _pendingVolume = v
        _volSetDebounce.restart()
    }

    Timer {
        id: _volSetDebounce
        interval: 50
        onTriggered: {
            volSetProc.command = ["wpctl", "set-volume", "@DEFAULT_AUDIO_SINK@", (_pendingVolume / 100).toFixed(2)]
            volSetProc.running = true
        }
    }

    property int _pendingBrightness: -1

    function setBrightness(v) {
        brightness = v
        _pendingBrightness = v
        _brightSetDebounce.restart()
    }

    Timer {
        id: _brightSetDebounce
        interval: 200
        onTriggered: {
            brightSetProc.command = ["ddcutil", "setvcp", "10", "" + _pendingBrightness]
            brightSetProc.running = true
        }
    }

    function doMedia(action) {
        if (!currentPlayer) return
        mediaActionProc.command = ["playerctl", "-p", currentPlayer, action]
        mediaActionProc.running = true
    }

    function seekMedia(pos) {
        if (!currentPlayer || mediaLen <= 0) return
        if (currentPlayer === "mpd" || currentPlayer.indexOf("mpd") !== -1) {
            mediaSeekProc.command = ["mpc", "seek", pos.toString()]
        } else {
            mediaSeekProc.command = ["playerctl", "-p", currentPlayer, "position", pos.toString()]
        }
        mediaSeekProc.running = true
    }

    function setGifIndex(idx) {
        gifIndex = idx
        saveSettings()
    }

    function setMediaDisplayMode(mode) {
        mediaDisplayMode = mode
        saveSettings()
    }

    function setMediaVinylWithArt(val) {
        mediaVinylWithArt = val
        saveSettings()
    }

    function setAnimationProfile(p) {
        animationProfile = p
        applyMangoAnimations()
        saveSettings()
    }

    function setBlurProfile(p) {
        blurProfile = p
        applyMangoBlur()
        saveSettings()
    }

    function setBarMode(mode) {
        barMode = mode
        saveSettings()
    }

    function setBorderRadius(r) {
        borderRadius = r
        borderRadiusProc.command = [
            "python3", Quickshell.env("HOME") + "/.config/mango/mango_config.py",
            "set-apply", "border_radius", "" + r
        ]
        borderRadiusProc.running = true
        saveSettings()
    }

    function applyMangoAnimations() {
        var configs = {
            "none": {
                type_open:    "slide",
                type_close:   "slide",
                layer_open:   "slide",
                layer_close:  "slide",
                fade_in:      "0",
                fade_out:     "0",
                zoom_initial: "1.0",
                zoom_end:     "1.0",
                open: "0", close: "0", move: "0", tag: "0", focus: "0",
                curve_open:    "0.0,0.0,1.0,1.0",
                curve_close:   "0.0,0.0,1.0,1.0",
                curve_move:    "0.0,0.0,1.0,1.0",
                curve_tag:     "0.0,0.0,1.0,1.0",
                curve_focus:   "0.0,0.0,1.0,1.0",
                curve_fadein:  "0.0,0.0,1.0,1.0",
                curve_fadeout: "0.0,0.0,1.0,1.0",
                enabled: "0"
            },
            "snappy": {
                type_open:    "slide",
                type_close:   "slide",
                layer_open:   "slide",
                layer_close:  "slide",
                fade_in:      "1",
                fade_out:     "1",
                zoom_initial: "0.94",
                zoom_end:     "1.0",
                open: "240", close: "180", move: "220", tag: "260", focus: "140",
                curve_open:    "0.25,0.1,0.25,1.0",
                curve_close:   "0.5,0.0,0.75,1.0",
                curve_move:    "0.3,0.0,0.3,1.0",
                curve_tag:     "0.25,0.1,0.25,1.0",
                curve_focus:   "0.4,0.0,0.2,1.0",
                curve_fadein:  "0.25,0.1,0.25,1.0",
                curve_fadeout: "0.5,0.0,0.75,1.0",
                enabled: "1"
            },
            "calm": {
                type_open:    "slide",
                type_close:   "slide",
                layer_open:   "slide",
                layer_close:  "slide",
                fade_in:      "1",
                fade_out:     "1",
                zoom_initial: "0.90",
                zoom_end:     "1.0",
                open: "480", close: "300", move: "420", tag: "720", focus: "240",
                curve_open:    "0.16,1.0,0.3,1.0",
                curve_close:   "0.4,0.0,0.2,1.0",
                curve_move:    "0.2,1.0,0.3,1.0",
                curve_tag:     "0.18,1.0,0.3,1.0",
                curve_focus:   "0.4,0.0,0.2,1.0",
                curve_fadein:  "0.16,1.0,0.3,1.0",
                curve_fadeout: "0.4,0.0,0.6,1.0",
                enabled: "1"
            },
            "bubbly": {
                type_open:    "slide",
                type_close:   "slide",
                layer_open:   "slide",
                layer_close:  "slide",
                fade_in:      "1",
                fade_out:     "1",
                zoom_initial: "0.82",
                zoom_end:     "1.02",
                open: "350", close: "220", move: "320", tag: "380", focus: "180",
                curve_open:    "0.05,1.15,0.15,1.0",
                curve_close:   "0.0,0.0,0.15,1.0",
                curve_move:    "0.08,1.12,0.18,1.02",
                curve_tag:     "0.05,1.15,0.15,1.02",
                curve_focus:   "0.0,0.0,0.15,1.0",
                curve_fadein:  "0.05,1.15,0.15,1.0",
                curve_fadeout: "0.0,0.0,0.15,1.0",
                enabled: "1"
            },
            "extraslow": {
                type_open:    "slide",
                type_close:   "slide",
                layer_open:   "slide",
                layer_close:  "slide",
                fade_in:      "1",
                fade_out:     "1",
                zoom_initial: "0.92",
                zoom_end:     "1.0",
                open: "640", close: "480", move: "560", tag: "800", focus: "360",
                curve_open:    "0.4,0.0,0.2,1.0",
                curve_close:   "0.4,0.0,0.6,1.0",
                curve_move:    "0.4,0.0,0.2,1.0",
                curve_tag:     "0.4,0.0,0.2,1.0",
                curve_focus:   "0.4,0.0,0.6,1.0",
                curve_fadein:  "0.4,0.0,0.2,1.0",
                curve_fadeout: "0.4,0.0,0.6,1.0",
                enabled: "1"
            }
        }

        var cfg = configs[animationProfile] || configs["bubbly"]

        var pairs = {
            "animations": cfg.enabled,
            "layer_animations": cfg.enabled,
            "animation_fade_in": cfg.fade_in,
            "animation_fade_out": cfg.fade_out,
            "animation_type_open": cfg.type_open,
            "animation_type_close": cfg.type_close,
            "layer_animation_type_open": cfg.layer_open,
            "layer_animation_type_close": cfg.layer_close,
            "zoom_initial_ratio": cfg.zoom_initial,
            "zoom_end_ratio": cfg.zoom_end,
            "animation_duration_open": cfg.open,
            "animation_duration_close": cfg.close,
            "animation_duration_move": cfg.move,
            "animation_duration_tag": cfg.tag,
            "animation_duration_focus": cfg.focus,
            "animation_curve_open": cfg.curve_open,
            "animation_curve_close": cfg.curve_close,
            "animation_curve_move": cfg.curve_move,
            "animation_curve_tag": cfg.curve_tag,
            "animation_curve_focus": cfg.curve_focus,
            "animation_curve_opafadein": cfg.curve_fadein,
            "animation_curve_opafadeout": cfg.curve_fadeout
        }

        mangoAnimProc.command = [
            "python3", Quickshell.env("HOME") + "/.config/mango/mango_config.py",
            "set-module", "animations", JSON.stringify(pairs), "--reload"
        ]
        mangoAnimProc.running = true
    }

    function applyMangoBlur() {
        var configs = {
            "frosted": {
                enabled: "1", layer: "1", optimized: "0",
                passes: "4", radius: "14",
                noise: "0.02", brightness: "0.9", contrast: "0.9", saturation: "1.2"
            },
            "balanced": {
                enabled: "1", layer: "1", optimized: "0",
                passes: "3", radius: "10",
                noise: "0.02", brightness: "0.9", contrast: "0.9", saturation: "1.2"
            },
            "subtle": {
                enabled: "1", layer: "1", optimized: "0",
                passes: "2", radius: "8",
                noise: "0.02", brightness: "0.9", contrast: "0.9", saturation: "1.2"
            },
            "none": {
                enabled: "1", layer: "1", optimized: "0",
                passes: "0", radius: "0",
                noise: "0.02", brightness: "0.9", contrast: "0.9", saturation: "1.2"
            }
        }

        var cfg = configs[blurProfile] || configs["balanced"]

        var pairs = {
            "blur": cfg.enabled,
            "blur_layer": cfg.layer,
            "blur_optimized": cfg.optimized,
            "blur_params_num_passes": cfg.passes,
            "blur_params_radius": cfg.radius,
            "blur_params_noise": cfg.noise,
            "blur_params_brightness": cfg.brightness,
            "blur_params_contrast": cfg.contrast,
            "blur_params_saturation": cfg.saturation
        }

        mangoBlurProc.command = [
            "python3", Quickshell.env("HOME") + "/.config/mango/mango_config.py",
            "set-module", "blur", JSON.stringify(pairs), "--reload"
        ]
        mangoBlurProc.running = true
    }

    property string _mediaCmd: ""
    onLastActivePlayerChanged: _mediaCmd = _buildMediaCmd()

    function _buildMediaCmd() {
        var last = lastActivePlayer
        return [
            "last='" + last + "'",
            "player=''",
            "for p in $(playerctl -l 2>/dev/null); do",
            "  st=$(playerctl -p \"$p\" status 2>/dev/null)",
            "  [ \"$st\" = \"Playing\" ] && player=\"$p\" && break",
            "done",
            "if [ -z \"$player\" ] && [ -n \"$last\" ]; then",
            "  st=$(playerctl -p \"$last\" status 2>/dev/null)",
            "  [ \"$st\" = \"Paused\" ] && player=\"$last\"",
            "fi",
            "if [ -z \"$player\" ]; then",
            "  for p in $(playerctl -l 2>/dev/null); do",
            "    st=$(playerctl -p \"$p\" status 2>/dev/null)",
            "    [ \"$st\" = \"Paused\" ] && player=\"$p\" && break",
            "  done",
            "fi",
            "[ -z \"$player\" ] && echo 'stopped|||0|0|' && exit 0",
            "s=$(playerctl -p \"$player\" status 2>/dev/null)",
            "art=$(playerctl -p \"$player\" metadata artist 2>/dev/null)",
            "ttl=$(playerctl -p \"$player\" metadata title 2>/dev/null)",
            "pos=$(playerctl -p \"$player\" position 2>/dev/null | cut -d. -f1)",
            "len=$(playerctl -p \"$player\" metadata mpris:length 2>/dev/null)",
            "len=$((len / 1000000))",
            "arturl=$(playerctl -p \"$player\" metadata mpris:artUrl 2>/dev/null)",
            "echo \"$player|$s|$art|$ttl|$pos|$len|$arturl\""
        ].join("\n")
    }

    onMediaStateChanged: {
        if (mediaState !== "playing") cavaDecaying = true
    }

    Process {
        id: mpProc
        stdout: SplitParser {
            onRead: data => {
                var line = data.trim()
                if (line.startsWith("stopped|")) {
                    mediaState   = "stopped"
                    mediaTitle   = ""
                    mediaArtist  = ""
                    mediaDisplay = ""
                    mediaPos     = 0
                    mediaLen     = 0
                    mediaArtUrl  = ""
                    return
                }
                var p = line.split("|")
                if (p.length >= 7) {
                    var newPlayer = p[0]
                    var newState  = p[1].toLowerCase()
                    currentPlayer = newPlayer
                    mediaState    = newState
                    mediaArtist   = p[2]
                    mediaTitle    = p[3]
                    if (!blockMediaPosUpdate) mediaPos = parseInt(p[4]) || 0
                    mediaLen      = parseInt(p[5]) || 0
                    mediaArtUrl   = p[6] || ""
                    if (newState === "playing" && newPlayer !== lastActivePlayer)
                        lastActivePlayer = newPlayer
                    var x = mediaTitle
                    if (mediaArtist) x = mediaArtist + " - " + mediaTitle
                    mediaDisplay = x
                }
            }
        }
    }

    Timer {
        interval: 2000; running: true; repeat: true; triggeredOnStart: true
        onTriggered: { mpProc.command = ["bash", "-c", _mediaCmd]; mpProc.running = true }
    }

    Timer {
        interval: 1000; running: mediaState === "playing"; repeat: true
        onTriggered: { if (!blockMediaPosUpdate && mediaPos < mediaLen) mediaPos += 1 }
    }

    Process {
        id: cavaProc
        running: mediaState === "playing"
        command: ["cava", "-p", Quickshell.env("HOME") + "/.config/cava/config_raw"]
        stdout: SplitParser {
            onRead: data => {
                var p = data.trim().split(";")
                var v = []
                for (var i = 0; i < 12 && i < p.length; i++) v.push(parseInt(p[i]) / 255)
                while (v.length < 12) v.push(0)
                cava = v
            }
        }
        onExited: { if (mediaState === "playing") cavaRestart.start() }
    }

    Timer { id: cavaRestart; interval: 1500; onTriggered: { if (mediaState === "playing") cavaProc.running = true } }

    Timer {
        interval: 60; running: cavaDecaying; repeat: true
        onTriggered: {
            var v = [], done = true
            for (var i = 0; i < 12; i++) {
                var val = cava[i] * 0.72
                if (val > 0.008) { v.push(val); done = false }
                else v.push(0)
            }
            cava = v
            if (done) cavaDecaying = false
        }
    }

    Process { id: mediaActionProc; onExited: { mpProc.command = ["bash", "-c", _mediaCmd]; mpProc.running = true } }
    Process { id: mediaSeekProc }

    function loadAppUsage() {
        appUsageLoadProc.running = true
    }

    function saveAppUsage() {
        var escaped = JSON.stringify(appUsage).replace(/'/g, "'\\''")
        appUsageSaveProc.command = ["bash", "-c", "printf '%s' '" + escaped + "' > " + _appUsagePath]
        appUsageSaveProc.running = true
    }

    function recordAppLaunch(appId) {
        var usage = Object.assign({}, appUsage)
        if (!usage[appId]) usage[appId] = { launches: 0, lastUsed: 0 }
        usage[appId].launches += 1
        usage[appId].lastUsed = Date.now()
        appUsage = usage
        saveAppUsage()
    }

    function getAppScore(appId) {
        var u = appUsage[appId]
        if (!u) return 0
        var launches     = u.launches || 0
        var daysSince    = (Date.now() - (u.lastUsed || 0)) / (1000 * 60 * 60 * 24)
        var recencyBonus = Math.max(0, 100 - daysSince * 10)
        return launches * 4 + recencyBonus
    }

    function colorToMango(c) {
        var hex = toHex(c)
        return "0x" + hex.substring(1) + "ff"
    }

    function applyKittyOpacity() {
        kittyOpacityProc.command = ["bash", "-c",
            "for s in /tmp/kitty-socket-*; do " +
            "kitty @ --to unix:$s set-background-opacity " +
            (transparencyEnabled ? "0.8" : "1.0") +
            " 2>/dev/null & done; wait"]
        kittyOpacityProc.running = true
    }

    function writeKittyConf() {
        var bg      = toHex(Colors.bg)
        var fg      = toHex(Colors.fg)
        var accent  = toHex(Colors.accent)
        var surface = toHex(Colors.surface)
        var dim     = toHex(Colors.dim)
        var red     = toHex(Colors.red)
        var green   = toHex(Colors.green)
        var yellow  = toHex(Colors.yellow)
        var opacity = transparencyEnabled ? "0.8" : "1.0"

        kittyConfProc.command = ["bash", "-c", [
            "cat > " + _kittyColorsPath + " << 'KITTYEOF'",
            "background_opacity " + opacity,
            "foreground "               + fg,
            "background "               + bg,
            "cursor "                   + accent,
            "cursor_text_color "        + bg,
            "selection_foreground "     + bg,
            "selection_background "     + accent,
            "url_color "                + accent,
            "active_tab_foreground "    + bg,
            "active_tab_background "    + accent,
            "inactive_tab_foreground "  + dim,
            "inactive_tab_background "  + surface,
            "active_border_color "      + accent,
            "inactive_border_color "    + surface,
            "color0 "  + surface,
            "color8 "  + dim,
            "color1 "  + red,
            "color9 "  + red,
            "color2 "  + green,
            "color10 " + green,
            "color3 "  + yellow,
            "color11 " + yellow,
            "color4 "  + accent,
            "color12 " + accent,
            "color5 "  + accent,
            "color13 " + accent,
            "color6 "  + accent,
            "color14 " + accent,
            "color7 "  + fg,
            "color15 " + fg,
            "KITTYEOF"
        ].join("\n")]
        kittyConfProc.running = true
    }

    function updateMangoOpacity() {
        var unfocused = transparencyEnabled ? "0.85" : "1.0"
        mangoOpacityProc.command = [
            "python3", Quickshell.env("HOME") + "/.config/mango/mango_config.py",
            "set-many", JSON.stringify({"focused_opacity": "1.0", "unfocused_opacity": unfocused}), "--apply"
        ]
        mangoOpacityProc.running = true
    }

    function updateMangoBorderColors() {
        var pairs = {
            "focuscolor":     colorToMango(Colors.accent),
            "urgentcolor":    colorToMango(Colors.red),
            "scratchpadcolor": colorToMango(Colors.accent),
            "globalcolor":    colorToMango(Colors.accent),
            "overlaycolor":   colorToMango(Colors.green),
            "maximizescreencolor": colorToMango(Colors.yellow),
            "bordercolor":    colorToMango(Colors.dim)
        }

        mangoBorderProc.command = [
            "python3", Quickshell.env("HOME") + "/.config/mango/mango_config.py",
            "set-module", "colors", JSON.stringify(pairs), "--reload"
        ]
        mangoBorderProc.running = true
    }

    function saveSettings() {
        var data = {
            darkMode:            darkMode,
            darkModeLocked:      darkModeLocked,
            transparencyEnabled: transparencyEnabled,
            dndEnabled:          dndEnabled,
            pfpIndex:            pfpIndex,
            gifIndex:            gifIndex,
            mediaDisplayMode:    mediaDisplayMode,
            mediaVinylWithArt:   mediaVinylWithArt,
            animationProfile:    animationProfile,
            blurProfile:         blurProfile,
            barMode:             barMode,
            borderRadius:        borderRadius,
            wallhavenApiKey:     wallhavenApiKey,
            wallhavenSorting:    wallhavenSorting,
            wallhavenCategories: wallhavenCategories
        }
        var escaped = JSON.stringify(data).replace(/'/g, "'\\''")
        saveProc.command = ["bash", "-c", "printf '%s' '" + escaped + "' > " + _settingsPath]
        saveProc.running = true
    }

    function loadSettings() {
        loadProc.running = true
    }

    Process { id: saveProc }
    Process { id: kittyOpacityProc }
    Process { id: kittyStepProc }
    Process { id: kittyConfProc }
    Process { id: mangoOpacityProc }
    Process { id: mangoBorderProc }
    Process { id: appUsageSaveProc }
    Process { id: borderRadiusProc }
    Process { id: mangoAnimProc }
    Process { id: mangoBlurProc }

    Process {
        id: appUsageLoadProc
        command: ["cat", _appUsagePath]
        stdout: SplitParser {
            splitMarker: ""
            onRead: data => {
                try { appUsage = JSON.parse(data.trim()) }
                catch(e) { appUsage = {} }
            }
        }
    }

    Process {
        id: loadProc
        command: ["cat", _settingsPath]
        stdout: SplitParser {
            splitMarker: ""
            onRead: data => {
                try {
                    var s = JSON.parse(data.trim())
                    if (s.darkMode !== undefined) {
                        darkMode = s.darkMode
                        Colors.darkMode = s.darkMode
                        Colors.autoMode = false
                    }
                    if (s.darkModeLocked      !== undefined) darkModeLocked      = s.darkModeLocked
                    if (s.transparencyEnabled !== undefined) transparencyEnabled = s.transparencyEnabled
                    if (s.dndEnabled          !== undefined) dndEnabled          = s.dndEnabled
                    if (s.pfpIndex            !== undefined) pfpIndex            = s.pfpIndex
                    if (s.gifIndex            !== undefined) gifIndex            = s.gifIndex
                    if (s.mediaDisplayMode    !== undefined) mediaDisplayMode    = s.mediaDisplayMode
                    if (s.mediaVinylWithArt   !== undefined) mediaVinylWithArt   = s.mediaVinylWithArt
                    if (s.animationProfile    !== undefined) {
                        animationProfile = s.animationProfile
                        Animations.profile = s.animationProfile
                    }
                    if (s.blurProfile  !== undefined) blurProfile  = s.blurProfile
                    if (s.barMode      !== undefined) barMode      = s.barMode
                    if (s.borderRadius !== undefined) borderRadius = s.borderRadius
                    if (s.wallhavenApiKey !== undefined) wallhavenApiKey = s.wallhavenApiKey
                    if (s.wallhavenSorting !== undefined) wallhavenSorting = s.wallhavenSorting
                    if (s.wallhavenCategories !== undefined) wallhavenCategories = s.wallhavenCategories
                    initDelay.start()
                } catch(e) {}
            }
        }
    }

    Timer {
        id: initDelay
        interval: 800
        onTriggered: {
            applyKittyOpacity()
            writeKittyConf()
        }
    }

    Process { id: volSetProc }
    Process { id: brightSetProc }

    Process {
        id: volWatch
        command: ["pactl", "subscribe"]
        running: true
        stdout: SplitParser { onRead: data => { if (data.includes("sink")) volDebounce.restart() } }
        onExited: volWatchRestart.start()
    }

    Timer { id: volWatchRestart; interval: 1000; onTriggered: volWatch.running = true }
    Timer { id: volDebounce; interval: 30; onTriggered: volReadProc.running = true }

    Process {
        id: volReadProc
        command: ["bash", "-c",
            "v=$(wpctl get-volume @DEFAULT_AUDIO_SINK@ 2>/dev/null); " +
            "m=$(echo \"$v\" | grep -q MUTED && echo 1 || echo 0); " +
            "p=$(echo \"$v\" | awk '{printf \"%.0f\", $2 * 100}'); " +
            "echo \"$p|$m\""]
        running: true
        stdout: SplitParser {
            onRead: data => {
                var p = data.trim().split("|")
                ui.volume = parseInt(p[0]) || 0
                ui.muted  = p[1] === "1"
            }
        }
    }

    Process {
        id: brightReadProc
        command: ["bash", "-c", "ddcutil getvcp 10 | grep -oP 'current value =\\s*\\K\\d+' || echo 100"]
        running: true
        stdout: SplitParser { onRead: data => ui.brightness = parseInt(data) || 100 }
    }

    Process {
        id: brightWatch
        command: ["true"]
        running: false
    }

    Timer { id: brightWatchRestart; interval: 1000; onTriggered: brightWatch.running = true }
    Timer { id: brightDebounce; interval: 50; onTriggered: brightReadProc.running = true }

    Behavior on barOpacity { NumberAnimation { duration: 300; easing.type: Easing.OutCubic } }
}